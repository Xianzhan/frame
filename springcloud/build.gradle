buildscript {
    ext {
        springBootVersion = '2.0.5.RELEASE'
        springCloudVersion = 'Finchley.SR3'
    }
    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        jcenter()
        mavenCentral()
    }

    dependencies {// 用来打包
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'java'
}

group 'xianzhan'
version '1.0'
description 'Spring Cloud 框架 demo'

sourceCompatibility = 14
targetCompatibility = 14

allprojects {
    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        jcenter()
        mavenCentral()
    }
    // 指定编码格式
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
}

// 设定子模块的通用配置
subprojects {

    apply plugin: 'java'
    apply plugin: 'idea'
    // spring boot 插件
    apply plugin: 'org.springframework.boot'
    // Gradle 插件，提供类似 Maven 的依赖关系管理功能
    apply plugin: 'io.spring.dependency-management'

    dependencies {
        // JDK 9 以上需要此依赖
        compile group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '2.3.2'

        // Actuator 提供服务监控与管理的功能, 路径: /actuator/{端点}
        compile('org.springframework.boot:spring-boot-starter-actuator')

        testCompile('org.springframework.boot:spring-boot-starter-test')
    }

    dependencyManagement {
        imports {
            // spring bom 帮助我们在不指定版本号的情况下声明依赖项。
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    jar {
        manifest.attributes provider: 'gradle'
    }
}
